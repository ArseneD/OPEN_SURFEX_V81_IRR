!SFX_LIC Copyright 1994-2014 CNRS, Meteo-France and Universite Paul Sabatier
!SFX_LIC This is part of the SURFEX software governed by the CeCILL-C licence
!SFX_LIC version 1. See LICENSE, CeCILL-C_V1-en.txt and CeCILL-C_V1-fr.txt  
!SFX_LIC for details. version 1.

!     ##########################
      SUBROUTINE BUDGET_COUPL_ROUT (DE, DEC, DC, DMI, IO, NP, NPE, U, KNI, KFORC_STEP)
!     ##########################
!
!!
!!    PURPOSE
!!    -------
!        
!     
!!**  METHOD
!!    ------
!
!!    EXTERNAL
!!    --------
!!
!!    none
!!
!!    IMPLICIT ARGUMENTS
!!    ------------------ 
!!      
!!    REFERENCE
!!    ---------
!!     
!!    AUTHOR
!!    ------
!!
!!    L. Bouilloud &  B. Vincendon      * Meteo-France *
!!
!!    MODIFICATIONS
!!    -------------
!!
!!      Original  03/2008 
!!      03/2014: Modif BV : add more variables
!-------------------------------------------------------------------------------
!
!*       0.     DECLARATIONS
!               ------------
!
USE MODD_DIAG_n, ONLY : DIAG_t
USE MODD_DIAG_EVAP_ISBA_n, ONLY : DIAG_EVAP_ISBA_t
USE MODD_DIAG_MISC_ISBA_n, ONLY : DIAG_MISC_ISBA_t
USE MODD_ISBA_OPTIONS_n, ONLY : ISBA_OPTIONS_t
USE MODD_ISBA_n, ONLY : ISBA_NP_t, ISBA_NPE_t
USE MODD_SURF_ATM_n, ONLY : SURF_ATM_t
!
USE MODD_BUDGET_COUPL_ROUT ! contains all useful variables XB_*
!
USE MODD_TOPODYN,       ONLY : NNCAT, XQTOT, XTOPD_STEP, XQB_DR, XQB_RUN
USE MODD_COUPLING_TOPD, ONLY : XRUNOFF_TOP, XRUN_TOROUT, XDR_TOROUT, XATOP, NNB_TOPD

!
USE MODD_SURF_PAR
USE MODD_CSTS,            ONLY : XRHOLW
!
USE MODD_FORC_ATM,        ONLY : XSNOW       ,&! snow precipitation                    (kg/m2/s)
                                 XRAIN         ! liquid precipitation                  (kg/m2/s)
!
USE MODI_UNPACK_SAME_RANK
USE MODI_PACK_SAME_RANK 
USE MODI_AVG_PATCH_WG
USE MODI_DG_DFTO3L
!USE MODI_WRITE_DISCHARGE_FILE_PRTEST
!
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
USE PARKIND1  ,ONLY : JPRB
!
IMPLICIT NONE
!
!*      0.1    declarations of arguments
!
TYPE(DIAG_t), INTENT(INOUT) :: DC
TYPE(DIAG_EVAP_ISBA_t), INTENT(INOUT) :: DE
TYPE(DIAG_EVAP_ISBA_t), INTENT(INOUT) :: DEC
TYPE(DIAG_MISC_ISBA_t), INTENT(INOUT) :: DMI
TYPE(ISBA_OPTIONS_t), INTENT(INOUT) :: IO
TYPE(ISBA_NP_t), INTENT(INOUT) :: NP
TYPE(ISBA_NPE_t), INTENt(INOUT) :: NPE
TYPE(SURF_ATM_t), INTENT(INOUT) :: U
!
INTEGER, INTENT(IN)           :: KNI        ! expected physical size of full surface array
INTEGER, INTENT(IN)           :: KFORC_STEP ! time step
!
!*      0.2    declarations of local variables
REAL, DIMENSION(U%NSIZE_NATURE) :: ZINTER
REAL                          :: ZFACT0, ZFACT1, ZFACT2
INTEGER                       :: JMESH,JCAT
REAL, DIMENSION(U%NSIZE_NATURE,3)  :: ZWG_3L,ZWGI_3L,ZDG_3L          
REAL, DIMENSION(KNI)  :: ZB_DWGTOT,ZB_DWGITOT,ZB_DSWETOT,ZB_DWR,ZB_WATBUD         
!
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!-------------------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('BUDGET_COUPL_ROUT',0,ZHOOK_HANDLE)
!
!*       1.     Budget computation:
!               ---------------
IF (IO%CISBA=='DIF') THEN
 CALL DG_DFTO3L(IO, NP, ZDG_3L)
 ZWG_3L(:,2)=DMI%XFRD2_TWG(:)
 ZWG_3L(:,3)=DMI%XFRD3_TWG(:)
 ZWGI_3L(:,2)=DMI%XFRD2_TWGI(:)
 ZWGI_3L(:,3)=DMI%XFRD3_TWGI(:)

ELSEIF (IO%CISBA=='3-L') THEN
 CALL AVG_PATCH_WG(IO, NP, NPE, ZWG_3L, ZWGI_3L, ZDG_3L)
ENDIF
!
CALL UNPACK_SAME_RANK(U%NR_NATURE,DE%XRAINFALL(:), XB_RAIN)
CALL UNPACK_SAME_RANK(U%NR_NATURE,DE%XSNOWFALL(:), XB_SNOW)
!
CALL UNPACK_SAME_RANK(U%NR_NATURE,DE%XDWR, ZB_DWR)
CALL UNPACK_SAME_RANK(U%NR_NATURE,DC%XEVAP, XB_EVAP)        
!
CALL UNPACK_SAME_RANK(U%NR_NATURE,DEC%XRUNOFF,XB_RUNOFF_ISBA)
CALL UNPACK_SAME_RANK(U%NR_NATURE,DEC%XHORT,XB_HORTON)
CALL UNPACK_SAME_RANK(U%NR_NATURE,DE%XWATBUD, ZB_WATBUD)
XB_RUNOFF_TOPD = XB_RUNOFF_ISBA
CALL UNPACK_SAME_RANK(U%NR_NATURE,DEC%XDRAIN, XB_DRAIN)
!
CALL UNPACK_SAME_RANK(U%NR_NATURE,ZWG_3L(:,2)  ,XB_WG2)
CALL UNPACK_SAME_RANK(U%NR_NATURE,ZWG_3L(:,3)  ,XB_WG3)
WHERE ( XB_WG2/=XUNDEF .AND. XB_DG2/=XUNDEF .AND. XB_WG3/=XUNDEF .AND. XB_DG3/=XUNDEF )
  XB_WGTOT(:) = XB_WG2(:)*XB_DG2(:) + XB_WG3(:)*(XB_DG3(:)-XB_DG2(:)) !m3/m2
ELSEWHERE
  XB_WGTOT(:) = XUNDEF
ENDWHERE
!
CALL UNPACK_SAME_RANK(U%NR_NATURE,ZWGI_3L(:,2) ,XB_WGI2)
CALL UNPACK_SAME_RANK(U%NR_NATURE,ZWGI_3L(:,3) ,XB_WGI3)
WHERE ( XB_WGI2/=XUNDEF .AND. XB_DG2/=XUNDEF .AND. XB_WGI3/=XUNDEF .AND. XB_DG3/=XUNDEF )
  XB_WGITOT(:) = XB_WGI2(:)*XB_DG2(:) + XB_WGI3(:)*(XB_DG3(:)-XB_DG2(:)) !m3/m2
ELSEWHERE
  XB_WGITOT(:) = XUNDEF
ENDWHERE
!
CALL UNPACK_SAME_RANK(NP%AL(1)%NR_P,NPE%AL(1)%TSNOW%WSNOW(:,1),ZINTER)
CALL UNPACK_SAME_RANK(U%NR_NATURE,ZINTER,XB_SWE1)
!
CALL UNPACK_SAME_RANK(NP%AL(1)%NR_P,NPE%AL(1)%TSNOW%WSNOW(:,2),ZINTER)
CALL UNPACK_SAME_RANK(U%NR_NATURE,ZINTER,XB_SWE2)
!
CALL UNPACK_SAME_RANK(NP%AL(1)%NR_P,NPE%AL(1)%TSNOW%WSNOW(:,3),ZINTER)
CALL UNPACK_SAME_RANK(U%NR_NATURE,ZINTER,XB_SWE3)
!
XB_SWETOT(:) = XB_SWE1(:)+XB_SWE2(:)+XB_SWE3(:)
!
DO JCAT=1,NNCAT
  XB_QTOT(JCAT) = SUM(XQTOT(JCAT,1:KFORC_STEP))
  XB_QRUN(JCAT) = SUM(XQB_RUN(JCAT,1:KFORC_STEP))
  XB_QDR(JCAT)  = SUM(XQB_DR(JCAT,1:KFORC_STEP))
ENDDO
!
DO JCAT=1,NNCAT
  !
  DO JMESH=1,KNI
    !
    IF ( XB_DG2(JMESH)/=XUNDEF ) THEN
!!    Water going in the system
      ZFACT0 =  XB_ABV_BYMESH(JMESH,JCAT) * XB_MESH_SIZE(JMESH)
      ZFACT1 =  ZFACT0 / XRHOLW
      ZFACT2 =  NNB_TOPD * ZFACT1
      !ZFACT2 =  XTOPD_STEP * ZFACT1
      !
!!      variable =1 : Rain      
      XB_VAR_BV(KFORC_STEP,JCAT,1) = XB_VAR_BV(KFORC_STEP,JCAT,1) + XB_RAIN(JMESH) * ZFACT2
!!      variable =2 : Snow
      XB_VAR_BV(KFORC_STEP,JCAT,2) = XB_VAR_BV(KFORC_STEP,JCAT,2) + XB_SNOW(JMESH) * ZFACT2
!!    Water going out of the system
!!      variable =3 : Incerception 
      XB_VAR_BV(KFORC_STEP,JCAT,3) = XB_VAR_BV(KFORC_STEP,JCAT,3) + ZB_DWR(JMESH) * ZFACT2
!!      variable =4 : Evaporation
      XB_VAR_BV(KFORC_STEP,JCAT,4) = XB_VAR_BV(KFORC_STEP,JCAT,4) + (XB_EVAP(JMESH)-XB_EVAPM(JMESH)) * ZFACT1
!!      variable =5 : Runoff
      XB_VAR_BV(KFORC_STEP,JCAT,5) = XB_VAR_BV(KFORC_STEP,JCAT,5) + (XB_RUNOFF_TOPD(JMESH)-XB_RUNOFF_TOPDM(JMESH)) * ZFACT1
!!      variable =6 : Drainage
      XB_VAR_BV(KFORC_STEP,JCAT,6) = XB_VAR_BV(KFORC_STEP,JCAT,6) + (XB_DRAIN(JMESH)-XB_DRAINM(JMESH)) * ZFACT1
!!      variable =7 : Variation of liquid water stocked in the ground
      XB_VAR_BV(KFORC_STEP,JCAT,7) = XB_VAR_BV(KFORC_STEP,JCAT,7) + (XB_WGTOT(JMESH)-XB_WGTOTM(JMESH)) * ZFACT0   
!!      variable =8 : Variation of solid water stocked in the ground
      XB_VAR_BV(KFORC_STEP,JCAT,8) = XB_VAR_BV(KFORC_STEP,JCAT,8) + (XB_WGITOT(JMESH)-XB_WGITOTM(JMESH)) * ZFACT0 
!!      variable =9 : Variation of melting snow
      XB_VAR_BV(KFORC_STEP,JCAT,9) = XB_VAR_BV(KFORC_STEP,JCAT,9) + (XB_SWETOT(JMESH)-XB_SWETOTM(JMESH)) * ZFACT0 
!!      variable =11 : Hortonian Runoff
      XB_VAR_BV(KFORC_STEP,JCAT,11) = XB_VAR_BV(KFORC_STEP,JCAT,11) + (XB_HORTON(JMESH)-XB_HORTONM(JMESH)) * ZFACT1
!!      variable =12 : ISBA water budget
      XB_VAR_BV(KFORC_STEP,JCAT,12) = XB_VAR_BV(KFORC_STEP,JCAT,12) + ZB_WATBUD(JMESH) * ZFACT2

 !! bilan hors BV en m3
      ZFACT0 =  (1.-XB_ABV_BYMESH(JMESH,JCAT)) * XB_MESH_SIZE(JMESH)
      ZFACT1 =  ZFACT0 / XRHOLW
      ZFACT2 =  NNB_TOPD * ZFACT1
!      ZFACT2 =  XTOPD_STEP * ZFACT1
      !
      XB_VAR_NOBV(KFORC_STEP,JCAT,1) = XB_VAR_NOBV(KFORC_STEP,JCAT,1) + XB_RAIN(JMESH) * ZFACT2
      XB_VAR_NOBV(KFORC_STEP,JCAT,2) = XB_VAR_NOBV(KFORC_STEP,JCAT,2) + XB_SNOW(JMESH) * ZFACT2
      XB_VAR_NOBV(KFORC_STEP,JCAT,3) = XB_VAR_NOBV(KFORC_STEP,JCAT,3) + ZB_DWR(JMESH) * ZFACT2
      XB_VAR_NOBV(KFORC_STEP,JCAT,4) = XB_VAR_NOBV(KFORC_STEP,JCAT,4) + (XB_EVAP(JMESH)-XB_EVAPM(JMESH)) * ZFACT1
      XB_VAR_NOBV(KFORC_STEP,JCAT,5) = XB_VAR_NOBV(KFORC_STEP,JCAT,5) + (XB_RUNOFF_ISBA(JMESH)-XB_RUNOFF_ISBAM(JMESH)) * ZFACT1
      XB_VAR_NOBV(KFORC_STEP,JCAT,6) = XB_VAR_NOBV(KFORC_STEP,JCAT,6) + (XB_DRAIN(JMESH)-XB_DRAINM(JMESH)) * ZFACT1
      XB_VAR_NOBV(KFORC_STEP,JCAT,7) = XB_VAR_NOBV(KFORC_STEP,JCAT,7) + (XB_WGTOT(JMESH)-XB_WGTOTM(JMESH)) * ZFACT0   
      XB_VAR_NOBV(KFORC_STEP,JCAT,8) = XB_VAR_NOBV(KFORC_STEP,JCAT,8) + (XB_WGITOT(JMESH)-XB_WGITOTM(JMESH)) * ZFACT0   
      XB_VAR_NOBV(KFORC_STEP,JCAT,9) = XB_VAR_NOBV(KFORC_STEP,JCAT,9) + (XB_SWETOT(JMESH)-XB_SWETOTM(JMESH)) * ZFACT0 
      XB_VAR_NOBV(KFORC_STEP,JCAT,11) = XB_VAR_NOBV(KFORC_STEP,JCAT,11) + (XB_HORTON(JMESH)-XB_HORTONM(JMESH)) * ZFACT1
      XB_VAR_NOBV(KFORC_STEP,JCAT,12) = XB_VAR_NOBV(KFORC_STEP,JCAT,12) + ZB_WATBUD(JMESH) * ZFACT2
      !
    ENDIF
    !     
  ENDDO
  !
  XB_VAR_BV(KFORC_STEP,JCAT,10) = SUM(XB_VAR_BV(KFORC_STEP,JCAT,1:2)) - SUM(XB_VAR_BV(KFORC_STEP,JCAT,3:9))
  !
  XB_VAR_NOBV(KFORC_STEP,JCAT,10) =  SUM(XB_VAR_NOBV(KFORC_STEP,JCAT,1:2)) - SUM(XB_VAR_NOBV(KFORC_STEP,JCAT,3:9))
  !
  XB_VAR_Q(KFORC_STEP,JCAT,1) = (XB_QTOT(JCAT)-XB_QTOTM(JCAT)) * XTOPD_STEP
  XB_VAR_Q(KFORC_STEP,JCAT,2) = (XB_QRUN(JCAT)-XB_QRUNM(JCAT)) * XTOPD_STEP
  XB_VAR_Q(KFORC_STEP,JCAT,3) = (XB_QDR(JCAT)- XB_QDRM(JCAT)) * XTOPD_STEP
  XB_VAR_Q(KFORC_STEP,JCAT,4) = SUM(XRUN_TOROUT(JCAT,:)) * XTOPD_STEP
  XB_VAR_Q(KFORC_STEP,JCAT,5) = SUM(XDR_TOROUT(JCAT,:)) * XTOPD_STEP
  !
ENDDO   
!
!bilan tot isba (m3)
DO JMESH=1,KNI
  !
  IF (XB_DG2(JMESH)/=XUNDEF) THEN  
    !
    ZFACT0 =  XB_MESH_SIZE(JMESH)
    ZFACT1 =  ZFACT0 / XRHOLW
      ZFACT2 =  NNB_TOPD * ZFACT1
!    ZFACT2 =  XTOPD_STEP * ZFACT1
    !
!!    Water going in the system
!!      variable =1 : Rain
    XB_VAR_TOT(KFORC_STEP,1) = XB_VAR_TOT(KFORC_STEP,1) + XB_RAIN(JMESH) * ZFACT2
!!      variable =2 : Snow
    XB_VAR_TOT(KFORC_STEP,2) = XB_VAR_TOT(KFORC_STEP,2) + XB_SNOW(JMESH) * ZFACT2
!!    Water going out of the system
!!      variable =3 : Incerception 
    XB_VAR_TOT(KFORC_STEP,3) = XB_VAR_TOT(KFORC_STEP,3) + ZB_DWR(JMESH) * ZFACT2
!!      variable =4 : Evaporation
    XB_VAR_TOT(KFORC_STEP,4) = XB_VAR_TOT(KFORC_STEP,4) + (XB_EVAP(JMESH)-XB_EVAPM(JMESH)) * ZFACT1
!!      variable =5 : Runoff
    XB_VAR_TOT(KFORC_STEP,5) = XB_VAR_TOT(KFORC_STEP,5) + (XB_RUNOFF_ISBA(JMESH)-XB_RUNOFF_ISBAM(JMESH))  * ZFACT1 
!!      variable =6 : Drainage
    XB_VAR_TOT(KFORC_STEP,6) = XB_VAR_TOT(KFORC_STEP,6) + (XB_DRAIN(JMESH)-XB_DRAINM(JMESH)) * ZFACT1
!!      variable =7 : Variation of liquid water stocked in the ground
    XB_VAR_TOT(KFORC_STEP,7) = XB_VAR_TOT(KFORC_STEP,7) + (XB_WGTOT(JMESH)-XB_WGTOTM(JMESH)) * ZFACT0  
!!      variable =8 : Variation of solid water stocked in the ground
    XB_VAR_TOT(KFORC_STEP,8) = XB_VAR_TOT(KFORC_STEP,8) + (XB_WGITOT(JMESH)-XB_WGITOTM(JMESH)) * ZFACT0  
!!      variable =9 : Variation of melting snow
    XB_VAR_TOT(KFORC_STEP,9) = XB_VAR_TOT(KFORC_STEP,9) + (XB_SWETOT(JMESH)-XB_SWETOTM(JMESH)) * ZFACT0  
!!      variable =11: Hortonian runoff
    XB_VAR_TOT(KFORC_STEP,11) = XB_VAR_TOT(KFORC_STEP,11) + (XB_HORTON(JMESH)-XB_HORTONM(JMESH)) * ZFACT1
!!      variable =12 : ISBA water budget
    XB_VAR_TOT(KFORC_STEP,12) = XB_VAR_TOT(KFORC_STEP,12) + ZB_WATBUD(JMESH) * ZFACT2
   !
  ENDIF
  !
ENDDO !JMESH
!
XB_VAR_TOT(KFORC_STEP,10) = SUM(XB_VAR_TOT(KFORC_STEP,1:2)) - SUM(XB_VAR_TOT(KFORC_STEP,3:9)) 
!
!
XB_WRM  (:) = XB_WR(:)
XB_EVAPM(:) = XB_EVAP(:)
!
XB_RUNOFF_TOPDM(:) = XB_RUNOFF_TOPD(:)
XB_RUNOFF_ISBAM(:) = XB_RUNOFF_ISBA(:)
XB_HORTONM(:) = XB_HORTON(:)
XB_DRAINM      (:) = XB_DRAIN(:)
!
XB_WG2M   (:) = XB_WG2(:)
XB_WG3M   (:) = XB_WG3(:)
XB_WGTOTM (:) = XB_WGTOT(:)
XB_WGI2M  (:) = XB_WGI2(:)
XB_WGI3M  (:) = XB_WGI3(:)
XB_WGITOTM(:) = XB_WGITOT(:)
XB_SWE1M  (:) = XB_SWE1(:)
XB_SWE2M  (:) = XB_SWE2(:)
XB_SWE3M  (:) = XB_SWE3(:)
XB_SWETOTM(:) = XB_SWETOT(:)
!
XB_QTOTM(:) = XB_QTOT(:)
XB_QRUNM(:) = XB_QRUN(:) 
XB_QDRM (:) = XB_QDR(:)
!
IF (LHOOK) CALL DR_HOOK('BUDGET_COUPL_ROUT',1,ZHOOK_HANDLE)
!
END SUBROUTINE BUDGET_COUPL_ROUT
