!SFX_LIC Copyright 1994-2014 CNRS, Meteo-France and Universite Paul Sabatier
!SFX_LIC This is part of the SURFEX software governed by the CeCILL-C licence
!SFX_LIC version 1. See LICENSE, CeCILL-C_V1-en.txt and CeCILL-C_V1-fr.txt  
!SFX_LIC for details. version 1.
SUBROUTINE OI_FCTVEG(KH,PVEG,&
   PVGAT1,PVGAT2,PVGAT3,PVGBT1,PVGBT2,PVGBT3,PVGCT1,PVGCT2, &
   PVGAH1,PVGAH2,PVGAH3,PVGBH1,PVGBH2,PVGBH3,PVGCH1,PVGCH2, &
   PSIGT2MP,PSIGHP2,PG1,PG2,PG3,PG4,&
   PVGST,PVGSH,PVGPT1,PVGPH1,PVGPT2,PVGPH2)  
!
!****-------------------------------------------------------------------
!
USE MODD_ASSIM, ONLY : XSIGHP1, XSIGT2MR, XSIGH2MR, XSIGT2MO, XSIGH2MO 
!
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
USE PARKIND1  ,ONLY : JPRB
!
IMPLICIT NONE
!
INTEGER :: J
INTEGER, INTENT(IN) :: KH
!
REAL, DIMENSION(24), INTENT(IN) :: PVGAT1,PVGAT2,PVGAT3
REAL, DIMENSION(24), INTENT(IN) :: PVGBT1,PVGBT2,PVGBT3
REAL, DIMENSION(24), INTENT(IN) :: PVGCT1,PVGCT2
REAL, DIMENSION(24), INTENT(IN) :: PVGAH1,PVGAH2,PVGAH3
REAL, DIMENSION(24), INTENT(IN) :: PVGBH1,PVGBH2,PVGBH3
REAL, DIMENSION(24), INTENT(IN) :: PVGCH1,PVGCH2
REAL, DIMENSION(24), INTENT(IN) :: PSIGT2MP,PSIGHP2
!
REAL, INTENT(IN) :: PVEG
!
REAL, INTENT(OUT) :: PG1,PG2,PG3,PG4
REAL, INTENT(OUT) :: PVGST,PVGSH,PVGPT1,PVGPH1,PVGPT2,PVGPH2
!
REAL :: ZSIGH2MP
REAL :: ZX1,ZY1,ZX2,ZY2,ZX3,ZY3,ZX4,ZY4
!
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
!**---------------------------------------------------------------------
!**  1. Initialisation des polynomes bruts et des champs de reference.
!**     -------------------------------------------------------------
!
! fonction definissant la dependance par rapport aux erreurs d'observation
!REAL :: G
!REAL :: PX,PY
!G ( PX,PY ) = PX**2 / (PX**2 + PY**2 + (PX*PY)**2)
!
! ecart-type d'erreurs de prevision sur H2m
!
IF (LHOOK) CALL DR_HOOK('OI_FCTVEG',0,ZHOOK_HANDLE)
ZSIGH2MP  = XSIGHP1 + PSIGHP2(KH)*PVEG
!
!G1 = G(SIGH2MO/SIGH2MP(KH,PVEG),SIGT2MO/SIGT2MP(KH))
ZX1 = XSIGH2MO/ZSIGH2MP
ZY1 = XSIGT2MO/PSIGT2MP(KH)
PG1  = ZX1**2 / (ZX1**2 + ZY1**2 + (ZX1*ZY1)**2)
!
!G2= G(SIGH2MR/SIGH2MP(KH,PVEG),SIGT2MR/SIGT2MP(KH))
ZX2 = XSIGH2MR/ZSIGH2MP
ZY2 = XSIGT2MR/PSIGT2MP(KH)          
PG2  = ZX2**2 / (ZX2**2 + ZY2**2 + (ZX2*ZY2)**2)
!
!G3= G(SIGT2MO/SIGT2MP(KH),SIGH2MO/SIGH2MP(KH,PVEG))
ZX3 = XSIGT2MO/PSIGT2MP(KH)
ZY3 = XSIGH2MO/ZSIGH2MP
PG3  = ZX3**2 / (ZX3**2 + ZY3**2 + (ZX3*ZY3)**2)
!
!G4= G(SIGT2MR/SIGT2MP(KH),SIGH2MR/SIGH2MP(KH,PVEG))
ZX4 = XSIGT2MR/PSIGT2MP(KH)
ZY4 = XSIGH2MR/ZSIGH2MP          
PG4  = ZX4**2 / (ZX4**2 + ZY4**2 + (ZX4*ZY4)**2)
!
! polynomes de base pour la dependance par rapport a l'indice de vegetation
!
PVGST  = (1.0 - PVEG)*(PVGAT1(KH) + PVGAT2(KH)*PVEG + PVGAT3(KH)*PVEG**2)
PVGSH  = (1.0 - PVEG)*(PVGAH1(KH) + PVGAH2(KH)*PVEG + PVGAH3(KH)*PVEG**2)
PVGPT1 = (1.0 - PVEG)*(PVGBT1(KH) + PVGBT2(KH)*PVEG + PVGBT3(KH)*PVEG**2)
PVGPH1 = (1.0 - PVEG)*(PVGBH1(KH) + PVGBH2(KH)*PVEG + PVGBH3(KH)*PVEG**2)
PVGPT2 = PVEG*(PVGCT1(KH) + PVGCT2(KH)*PVEG)
PVGPH2 = PVEG*(PVGCH1(KH) + PVGCH2(KH)*PVEG)
IF (LHOOK) CALL DR_HOOK('OI_FCTVEG',1,ZHOOK_HANDLE)
!
!**---------------------------------------------------------------------
END SUBROUTINE OI_FCTVEG

